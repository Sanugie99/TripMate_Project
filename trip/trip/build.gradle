plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.korea'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

// React 프로젝트 디렉토리 설정
def frontendDir = new File(projectDir, '../../tripmate')

// Node.js 및 npm 실행을 위한 작업 정의
task npmInstall(type: Exec) {
    workingDir frontendDir
    inputs.dir frontendDir
    group = "node"
    // OS에 따라 실행할 명령어 설정
    if (System.getProperty("os.name").toLowerCase().contains("windows")) {
        commandLine "cmd", "/c", "npm", "install"
    } else {
        commandLine "npm", "install"
    }
}

task npmBuild(type: Exec) {
    dependsOn npmInstall
    workingDir frontendDir
    inputs.dir frontendDir
    group = "node"
    if (System.getProperty("os.name").toLowerCase().contains("windows")) {
        commandLine "cmd", "/c", "npm", "run", "build"
    } else {
        commandLine "npm", "run", "build"
    }
}

// Spring Boot 리소스 처리 전 React 빌드 실행
processResources {
    dependsOn npmBuild
    from("${frontendDir}/build") {
        into 'static'
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

tasks.named('test') {
	useJUnitPlatform()
}
